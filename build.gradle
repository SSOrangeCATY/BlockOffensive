plugins {
    id 'eclipse'
    id 'idea'
    id 'net.neoforged.moddev.legacyforge' version '2.0.140'
    id 'maven-publish'
}

group = mod_group_id
version = minecraft_version + "-" + file_version + "-" + mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}


legacyForge {
    version = "${minecraft_version}-${forge_version}"

    parchment {
        mappingsVersion = project.mapping_version
        minecraftVersion = project.mapping_mc_version
    }

    validateAccessTransformers = true

    runs {
        configureEach {
            gameDirectory = project.file('run')
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG

        }

        client {
            client()
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            server()
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
            programArgument '--nogui'
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            data()
            gameDirectory = project.file('run-data')
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }


    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }

}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"

    config "${mod_id}.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name 'Curse Maven'
        url "https://www.cursemaven.com"
    }
    maven {
        name 'IzzelAliz Maven'
        url 'https://maven.izzel.io/releases/'
    }
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
}

dependencies {
    additionalRuntimeClasspath(implementation("icyllis.modernui:ModernUI-Core:${modernui_core_version}")) {
        exclude group: "org.apache.logging.log4j", module: "log4j-core"
        exclude group: "org.apache.logging.log4j", module: "log4j-api"
        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j2-impl'
        exclude group: "com.google.code.findbugs", module: "jsr305"
        exclude group: "org.jetbrains", module: "annotations"
        exclude group: "com.ibm.icu", module: "icu4j"
        exclude group: "it.unimi.dsi", module: "fastutil"
    }
    additionalRuntimeClasspath(implementation("icyllis.modernui:ModernUI-Markdown:${modernui_core_version}")) {
        exclude group: "org.apache.logging.log4j", module: "log4j-core"
        exclude group: "org.apache.logging.log4j", module: "log4j-api"
        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j2-impl'
        exclude group: "com.google.code.findbugs", module: "jsr305"
        exclude group: "org.jetbrains", module: "annotations"
        exclude group: "com.ibm.icu", module: "icu4j"
        exclude group: "it.unimi.dsi", module: "fastutil"
    }

    modCompileOnly('org.apache.commons:commons-math3:3.6.1');

    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.3"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.5.3"))

    modImplementation("icyllis.modernui:ModernUI-Forge:${minecraft_version}-${modernui_forge_version}")
    modImplementation("maven.modrinth:timeless-and-classics-zero:${tacz_version}")
    modImplementation("maven.modrinth:lr-tactical:${lrtactical_version}")
    modImplementation("maven.modrinth:cloth-config:${cloth_config_version}")
    modImplementation("com.phasetranscrystal:fpsmatch") {
        transitive = false
    }

    modImplementation("maven.modrinth:counterstrikegrenade:${cs_grenade_version}")
    modRuntimeOnly("maven.modrinth:kotlin-for-forge:${kotlin_forge_version}")
    modImplementation("maven.modrinth:hit-indication:${hit_indication_version}")
    modImplementation("maven.modrinth:physicsmod:${physicsmod_version}")
    modImplementation("maven.modrinth:gd656killicon:${gd656killicon_version}")
    modImplementation("maven.modrinth:tacz-tweaks:${tacztweak_version}")
    modRuntimeOnly("maven.modrinth:yacl:${yacl_version}")
    modRuntimeOnly("maven.modrinth:jade:${jade_version}")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            forge_version          : forge_version,
            forge_version_range    : forge_version_range,
            loader_version_range   : loader_version_range,
            fpsm_version_range     : fpsm_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
sourceSets.main.resources.srcDir generateModMetadata
legacyForge.ideSyncTask generateModMetadata

jar {
    manifest.attributes([
            "MixinConfigs": "${mod_id}.mixins.json"
    ])
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = mod_group_id
            artifactId = mod_id
            version = mod_version

            artifact sourcesJar
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}